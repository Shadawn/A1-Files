Функция ДобавитьОписание(МассивОписаний, Знач ИмяКомпонента = Неопределено, Знач Владелец, РодительЭлемента = Неопределено, ПередЭлементом = Неопределено, Параметры = Неопределено, Действия = Неопределено) Экспорт
	Если ИмяКомпонента = Неопределено Тогда
		ИмяКомпонента = ИмяМодуля();
	КонецЕсли;
	Владелец = А1Э_Метаданные.СсылкаПоИдентификатору(Владелец);
	А1Э_УниверсальнаяФорма.ДобавитьОписаниеНастроекКомпонента(МассивОписаний, ИмяКомпонента,
	А1Э_Структуры.Создать(
	"А1Файлы_Владелец", Владелец, 
	));
	РабочиеПараметры = А1Э_Структуры.СкопироватьВШаблон(Параметры,
	"ТолькоПросмотр", Истина,
	"ИзменятьСоставСтрок", Ложь,
	"ИзменятьПорядокСтрок", Ложь,
	"ПоложениеСтрокиПоиска", ПоложениеСтрокиПоиска.Нет,
	);
	
	РабочиеДействия = А1Э_Структуры.Скопировать(Действия);
	
	А1Э_Формы.ДобавитьОписаниеВертикальнойГруппы(МассивОписаний, ИмяКомпонента, , РодительЭлемента, ПередЭлементом, ,
	А1Э_Структуры.Создать(
	"ФормаПриСозданииНаСервере", ИмяМодуля() + ".ФормаПриСозданииНаСервере",
	"ФормаПриЗаписиНаСервере", ИмяМодуля() + ".ФормаПриЗаписиНаСервере",
	));
	А1Э_Формы.ДобавитьОписаниеТаблицыФормы(МассивОписаний, ИмяТаблицы(ИмяКомпонента), ОписаниеКолонок(), , ИмяКомпонента, , РабочиеПараметры, РабочиеДействия);
	А1Файлы.ДобавитьОписаниеКнопкиПрисоединить(МассивОписаний, ИмяКомпонента + "___Добавить", Владелец, ИмяМодуля() + ".КомандаОбновитьДанные:НаСервере", , ИмяТаблицы(ИмяКомпонента) + "КоманднаяПанель");
	А1Э_Формы.ДобавитьОписаниеКомандыИКнопки2(МассивОписаний, ИмяКомпонента + "___Открыть", ИмяМодуля() + ".КомандаОткрыть", "Открыть", ИмяТаблицы(ИмяКомпонента) + "КоманднаяПанель"); 
	А1Э_Формы.ДобавитьОписаниеКомандыИКнопки2(МассивОписаний, ИмяКомпонента + "___Скачать", ИмяМодуля() + ".КомандаСкачать", "Скачать", ИмяТаблицы(ИмяКомпонента) + "КоманднаяПанель");
	А1Э_Формы.ДобавитьОписаниеКомандыИКнопки2(МассивОписаний, ИмяКомпонента + "___Удалить", ИмяМодуля() + ".КомандаУдалить," + ИмяМодуля() + ".КомандаОбновитьДанные:НаСервере", 
	"Удалить", ИмяТаблицы(ИмяКомпонента) + "КоманднаяПанель");
КонецФункции

Функция ОписаниеКолонок()
	Колонки = Новый Массив;
	Колонки.Добавить(А1Э_Формы.НовыйДанныеКолонкиТаблицыФормы("Картинка", "Строка", " ", А1Э_Структуры.Создать(
	"Ширина", 3,
	"РастягиватьПоГоризонтали", Ложь,
	"ПоложениеЗаголовка", ПоложениеЗаголовкаЭлементаФормы.Нет,
	)));
	Колонки.Добавить(А1Э_Формы.НовыйДанныеКолонкиТаблицыФормы("Файл", "Справочник.А1Файлы"));
	Колонки.Добавить(А1Э_Формы.НовыйДанныеКолонкиТаблицыФормы("Расширение", "Строка", , А1Э_Структуры.Создать(
	"Ширина", 6,
	"РастягиватьПоГоризонтали", Ложь,
	)));
	Колонки.Добавить(А1Э_Формы.НовыйДанныеКолонкиТаблицыФормы("Размер", "Число", "Размер (КВ)", А1Э_Структуры.Создать(
	"Ширина", 6,
	"РастягиватьПоГоризонтали", Ложь,
	)));
	Возврат Колонки;
КонецФункции 

#Если НЕ Клиент Тогда
	
	Функция ФормаПриСозданииНаСервере(ИмяКомпонента, Форма, Отказ, СтандартнаяОбработка) Экспорт
		Настройки = А1Э_УниверсальнаяФорма.НастройкиКомпонента(Форма, ИмяКомпонента);
		Настройки.Вставить("А1Файлы_Картинки", Картинки(Форма));
		Форма.Элементы[ИмяТаблицы(ИмяКомпонента) + "Картинка"].Вид = ВидПоляФормы.ПолеКартинки;
		ОбновитьДанные(Форма, ИмяКомпонента);
	КонецФункции
	
	Функция ФормаПриЗаписиНаСервере(ИмяКомпонента, Форма, Отказ, ТекущийОбъект, ПараметрыЗаписи) Экспорт 
		Настройки = А1Э_УниверсальнаяФорма.НастройкиКомпонента(Форма, ИмяКомпонента);
		Если ЗначениеЗаполнено(Настройки.А1Файлы_Владелец) Тогда Возврат Неопределено; КонецЕсли;
		
		Настройки.А1Файлы_Владелец = ТекущийОбъект.Ссылка;
	КонецФункции
	
	Функция ОбновитьДанные(Форма, ИмяКомпонента) Экспорт
		Таблица = Форма[ИмяТаблицы(ИмяКомпонента)];
		Таблица.Очистить();
		
		Настройки = А1Э_УниверсальнаяФорма.НастройкиКомпонента(Форма, ИмяКомпонента);
		ВладелецИдентификатор = А1Э_Метаданные.ИдентификаторПоСсылке(Настройки.А1Файлы_Владелец);
		Картинки = Настройки.А1Файлы_Картинки;
		
		УстановитьПривилегированныйРежим(Истина);
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПрисоединенныеФайлы.Файл КАК Файл,
		|	ПрисоединенныеФайлы.Файл.Расширение КАК Расширение,
		|	ПрисоединенныеФайлы.Файл.Размер КАК Размер
		|ИЗ
		|	РегистрСведений.А1Файлы_ПрисоединенныеФайлы КАК ПрисоединенныеФайлы
		|ГДЕ
		|	ПрисоединенныеФайлы.Объект = &Объект";
		Запрос.УстановитьПараметр("Объект", ВладелецИдентификатор);
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			Строка = Таблица.Добавить();
			ЗаполнитьЗначенияСвойств(Строка, Выборка);
			Строка.Размер = Цел(Строка.Размер / 1024);
			Строка.Картинка = Картинки[НРЕГ(Строка.Расширение)];
		КонецЦикла;
		
	КонецФункции
	
	Функция КомандаОбновитьДанные(Форма, ИмяКоманды) Экспорт
		Возврат ОбновитьДанные(Форма, А1Э_Строки.ПередСКонца(ИмяКоманды, "___"));
	КонецФункции
	
	Функция Картинки(Форма) Экспорт
		Результат = Новый Соответствие;
		АдресВорд = ПоместитьВоВременноеХранилище(БиблиотекаКартинок.А1Файлы_ФорматWord, Форма.УникальныйИдентификатор);
		АдресЭксель = ПоместитьВоВременноеХранилище(БиблиотекаКартинок.А1Файлы_ФорматExcel, Форма.УникальныйИдентификатор);
		АдресПДФ = ПоместитьВоВременноеХранилище(БиблиотекаКартинок.А1Файлы_ФорматPDF, Форма.УникальныйИдентификатор);
		АдресХТМЛ = ПоместитьВоВременноеХранилище(БиблиотекаКартинок.А1Файлы_ФорматHTML, Форма.УникальныйИдентификатор);
		АдресКартинка = ПоместитьВоВременноеХранилище(БиблиотекаКартинок.Картинка, Форма.УникальныйИдентификатор);
		Результат.Вставить("doc", АдресВорд);
		Результат.Вставить("docx", АдресВорд);
		Результат.Вставить("xls", АдресЭксель);
		Результат.Вставить("xlsx", АдресЭксель);
		Результат.Вставить("pdf", АдресПДФ);
		Результат.Вставить("jpg", АдресКартинка);
		Результат.Вставить("jpeg", АдресКартинка);
		Результат.Вставить("png", АдресКартинка);
		Результат.Вставить("bmp", АдресКартинка);
		Результат.Вставить("html", АдресХТМЛ);
		Результат.Вставить("htm", АдресХТМЛ);
		Возврат Результат;
	КонецФункции 
	
#КонецЕсли

#Если Клиент Тогда
	
	Функция КомандаОткрыть(Форма, Команда) Экспорт
		ИмяКомпонента = А1Э_Строки.ПередСКонца(Команда.Имя, "___");
		ТекущиеДанные = Форма.Элементы[ИмяТаблицы(ИмяКомпонента)].ТекущиеДанные;
		Если ТекущиеДанные = Неопределено Тогда
			Сообщить("Выберите строку в таблице файлов");
			Возврат Неопределено;
		КонецЕсли;
		АдресВХранилище = А1Э_ОбщееСервер.РезультатФункции("А1Файлы.ПоместитьВХранилище", ТекущиеДанные.Файл, Форма.УникальныйИдентификатор);
		А1Э_Файлы.Открыть(АдресВХранилище, "" + ТекущиеДанные.Файл + "." + ТекущиеДанные.Расширение);
	КонецФункции 
	
	Функция КомандаСкачать(Форма, Команда) Экспорт
		ИмяКомпонента = А1Э_Строки.ПередСКонца(Команда.Имя, "___");
		ТекущиеДанные = Форма.Элементы[ИмяТаблицы(ИмяКомпонента)].ТекущиеДанные;
		Если ТекущиеДанные = Неопределено Тогда
			Сообщить("Выберите строку в таблице файлов");
			Возврат Неопределено;
		КонецЕсли;
		#Если ВебКлиент Тогда
			АдресВХранилище = А1Э_ОбщееСервер.РезультатФункции("А1Файлы.ПоместитьВХранилище", ТекущиеДанные.Файл, Форма.УникальныйИдентификатор);
			А1Э_Файлы.Открыть(АдресВХранилище, "" + ТекущиеДанные.Файл + "." + ТекущиеДанные.Расширение);
		#Иначе
			А1Э_Файлы.ПоказатьВыборФайла("КомандаСкачатьПослеВыбора", ЭтотОбъект, А1Э_Структуры.Создать(
			"Форма", Форма,
			"Файл", ТекущиеДанные.Файл,
			"Режим", "Сохранение",
			"ПолноеИмяФайла", "" + ТекущиеДанные.Файл + "." + ТекущиеДанные.Расширение,
			"Расширение", ТекущиеДанные.Расширение, 
			))
		#КонецЕсли
	КонецФункции
	
	Функция КомандаСкачатьПослеВыбора(Результат, Контекст) Экспорт
		Если НЕ ЗначениеЗаполнено(Результат) Тогда Возврат Неопределено; КонецЕсли;
		 
		АдресВХранилище = А1Э_ОбщееСервер.РезультатФункции("А1Файлы.ПоместитьВХранилище", Контекст.Файл, Контекст.Форма.УникальныйИдентификатор);
		ПолучитьИзВременногоХранилища(АдресВХранилище).Записать(Результат[0]);
	КонецФункции 
	
	Функция КомандаУдалить(Форма, Команда) Экспорт
		ИмяКомпонента = А1Э_Строки.ПередСКонца(Команда.Имя, "___");
		ТекущиеДанные = Форма.Элементы[ИмяТаблицы(ИмяКомпонента)].ТекущиеДанные;
		Если ТекущиеДанные = Неопределено Тогда
			Сообщить("Выберите строку в таблице файлов");
			Возврат Неопределено;
		КонецЕсли;
		НастройкиКомпонента = А1Э_УниверсальнаяФорма.НастройкиКомпонента(Форма, ИмяКомпонента);
		А1Э_ОбщееСервер.РезультатФункции("А1Файлы.Отсоединить", ТекущиеДанные.Файл, НастройкиКомпонента.А1Файлы_Владелец);
	КонецФункции

#КонецЕсли
 
Функция ИмяТаблицы(ИмяКомпонента) 
	Возврат ИмяКомпонента + "___Таблица"; 
КонецФункции

Функция ИмяМодуля() Экспорт
	Возврат "А1Файлы_КомпонентТаблицаПрисоединенных";
КонецФункции 